module

public import Mathlib

public section

inductive Formula
| atom : ℕ → Formula
| bot  : Formula
| imp  : Formula → Formula → Formula
| box  : Formula → Formula
deriving Repr, DecidableEq

namespace Formula

prefix:100 "#" => atom
notation:90 "⊥" => bot
infixr:85 " 🡒 " => imp
prefix:95 "□" => box

abbrev neg (A : Formula) : Formula := A 🡒 ⊥
prefix:90 "∼" => neg

abbrev or (A B : Formula) : Formula := ∼A 🡒 B
infixl:83 " ⋎ " => or

abbrev and (A B : Formula) : Formula := ∼(A 🡒 ∼B)
infixl:84 " ⋏ " => and

end Formula

abbrev FormulaFinset := Finset Formula

abbrev FormulaFinset.box (Γ : FormulaFinset) : FormulaFinset := Γ.image (□·)


structure Sequent where
  ant : FormulaFinset
  suc : FormulaFinset

infix:50 " ⟹ " => Sequent.mk

inductive Proof : Sequent → Type
| axm (A) : Proof ({A} ⟹ {A})
| botL : Proof ({⊥} ⟹ ∅)
| wkL  {Γ Γ' Δ}  : Proof (Γ ⟹ Δ) → (_ : Γ ⊆ Γ' := by grind) → Proof (Γ' ⟹ Δ)
| wkR  {Γ Δ Δ'}  : Proof (Γ ⟹ Δ) → (_ : Δ ⊆ Δ' := by grind) → Proof (Γ ⟹ Δ')
| impL {Γ Δ A B} : Proof (Γ ⟹ (insert A Δ)) → Proof (insert B Γ ⟹ Δ) → Proof ((insert (A 🡒 B) Γ) ⟹ Δ)
| impR {Γ Δ A B} : Proof ((insert A Γ) ⟹ (insert B Δ)) → Proof (Γ ⟹ (insert (A 🡒 B) Δ))
| boxGL {Γ A} : Proof ((insert (□A) (Γ ∪ Γ.box)) ⟹ {A}) → Proof (Γ.box ⟹ {□A})

prefix:120 "⊢! " => Proof

namespace Proof

variable {Γ Δ : FormulaFinset} {A B C : Formula}

def union (A) {Γ Δ : Finset _} (hΓ : A ∈ Γ := by grind) (hΔ : A ∈ Δ := by grind) : ⊢! (Γ ⟹ Δ) := wkR $ wkL $ axm A

def botL_mem (h : ⊥ ∈ Γ := by grind) : ⊢! (Γ ⟹ Δ) := wkR (Δ := ∅) $ wkL botL

def mdpL_mem (A B) (h₁ : A 🡒 B ∈ Γ := by grind) (h₂ : A ∈ Γ := by grind) (h₃ : B ∈ Δ := by grind) : ⊢! (Γ ⟹ Δ) := by
  rw [(show Γ = insert (A 🡒 B) (insert A (Γ \ {A, A 🡒 B})) by grind)];
  apply impL;
  . apply union A;
  . apply union B;


def negL : ⊢! (Γ ⟹ (insert A Δ)) → ⊢! ((insert (∼A) Γ) ⟹ Δ) := λ p => impL p (wkR $ wkL botL)

def negR : ⊢! ((insert A Γ) ⟹ Δ) → ⊢! (Γ ⟹ (insert (∼A) Δ)) := λ p => impR $ wkR $ wkL p

def andL : ⊢! ((insert A $ insert B $ Γ) ⟹ Δ) → ⊢! (insert (A ⋏ B) Γ ⟹ Δ) := λ p => by
  apply impL;
  . apply impR;
    apply negR;
    simpa [(show (insert A $ insert B Γ) = (insert B $ insert A Γ) by grind)] using p;
  . exact botL_mem;

def andR : ⊢! (Γ ⟹ insert A Δ) → ⊢! (Γ ⟹ insert B Δ) → ⊢! (Γ ⟹ insert (A ⋏ B) Δ) := λ p q => by
  apply impR;
  apply impL;
  . exact wkR p;
  . exact negL $ wkR q;

def orL : ⊢! (insert A Γ ⟹ Δ) → ⊢! (insert B Γ ⟹ Δ) → ⊢! (insert (A ⋎ B) Γ ⟹ Δ) := λ p q => by
  apply impL;
  . exact negR p;
  . exact q;

def orR : ⊢! (Γ ⟹ (insert A $ insert B Δ)) → ⊢! (Γ ⟹ insert (A ⋎ B) Δ) := λ p => by
  apply impR;
  apply negL;
  simpa;


def axiomŁ1 : ⊢! (∅ ⟹ {A 🡒 B 🡒 A}) := impR (Δ := ∅) $ impR $ union A

def axiomŁ2 : ⊢! (∅ ⟹ {(A 🡒 B 🡒 C) 🡒 (A 🡒 B) 🡒 (A 🡒 C)}) := by
  apply impR (Δ := ∅);
  apply impR;
  apply impR;
  simp only [insert_empty_eq];
  rw [(show {A, A 🡒 B, A 🡒 B 🡒 C} = ({A 🡒 B 🡒 C, A 🡒 B, A}) by grind)];
  apply impL;
  . exact impL (union A) (union A);
  . exact impL (impL (union A) (union B)) (union C);

def axiomŁ3 : ⊢! (∅ ⟹ {(∼A 🡒 ∼B) 🡒 (B 🡒 A)}) := by
  apply impR (Δ := ∅);
  apply impR;
  simp;
  rw [(show {B, ∼A 🡒 ∼B} = ({∼A 🡒 ∼B, B}) by grind)];
  exact impL (negR $ union A) (negL $ union B);

def axiomK : ⊢! (∅ ⟹ {(□(A 🡒 B) 🡒 (□A 🡒 □B))}) := by
  apply impR (Δ := ∅);
  apply impR;
  simp only [insert_empty_eq];
  rw [(show ({□A, □(A 🡒 B)}) = (FormulaFinset.box {A, (A 🡒 B)}) by grind)];
  apply boxGL;
  apply mdpL_mem A B;

def axiom4 : ⊢! (∅ ⟹ {(□A 🡒 □□A)}) := by
  apply impR (Δ := ∅);
  simp only [insert_empty_eq];
  rw [(show ({□A}) = FormulaFinset.box {A} by grind)];
  apply boxGL;
  apply union (□A);

def axiomL : ⊢! (∅ ⟹ {□(□A 🡒 A) 🡒 □A}) := by
  apply impR (Δ := ∅);
  simp only [insert_empty_eq];
  rw [(show ({□(□A 🡒 A)}) = FormulaFinset.box {□A 🡒 A} by grind)];
  apply boxGL;
  apply mdpL_mem (□A) A;

def ruleNec : ⊢! (∅ ⟹ {A}) → ⊢! (∅ ⟹ {□A}) := λ p => boxGL (Γ := ∅) $ wkL p

#eval axiomŁ1 (A := #0) (B := #1)
#eval axiomŁ2 (A := #0) (B := #1) (C := #2)
#eval axiomŁ3 (A := #0) (B := #1)
#eval axiom4 (A := #0)
#eval axiomL (A := #0)

end Proof



abbrev Provable (S : Sequent) : Prop := Nonempty (⊢! S)
prefix:120 "⊢ " => Provable

namespace Provable

variable {Γ Δ : FormulaFinset} {A B C : Formula}

lemma axiomŁ1 : ⊢ (∅ ⟹ {A 🡒 B 🡒 A}) := ⟨Proof.axiomŁ1⟩
lemma axiomŁ2 : ⊢ (∅ ⟹ {(A 🡒 B 🡒 C) 🡒 (A 🡒 B) 🡒 (A 🡒 C)}) := ⟨Proof.axiomŁ2⟩
lemma axiomŁ3 : ⊢ (∅ ⟹ {(∼A 🡒 ∼B) 🡒 (B 🡒 A)}) := ⟨Proof.axiomŁ3⟩
lemma axiomK  : ⊢ (∅ ⟹ {(□(A 🡒 B) 🡒 (□A 🡒 □B))}) := ⟨Proof.axiomK⟩
lemma axiom4  : ⊢ (∅ ⟹ {(□A 🡒 □□A)}) := ⟨Proof.axiom4⟩
lemma axiomL  : ⊢ (∅ ⟹ {□(□A 🡒 A) 🡒 □A}) := ⟨Proof.axiomL⟩
lemma ruleNec : ⊢ (∅ ⟹ {A}) → ⊢ (∅ ⟹ {□A}) := λ ⟨p⟩ => ⟨Proof.ruleNec p⟩

end Provable

abbrev Unprovable (S : Sequent) : Prop := ¬⊢ S
prefix:120 "⊬ " => Unprovable


section Semantics

structure Model (κ : Type*) where
  Rel' : κ → κ → Prop
  Val : κ → ℕ → Prop

namespace Model

abbrev World (_ : Model κ) := κ
abbrev Rel {M : Model κ} : M.World → M.World → Prop := M.Rel'
infixl:60 " ≺ " => Rel

end Model

variable {M : Model κ} {A B : Formula} {Γ Γ' Δ Δ' : FormulaFinset}

@[grind]
def Formula.Forced {M : Model κ} (x : M.World) : Formula → Prop
| #a    => M.Val x a
| ⊥     => False
| A 🡒 B => Forced x A → Forced x B
| □A    => ∀ y, x ≺ y → Forced y A
infix:55 " ⊩ " => Formula.Forced

lemma Formula.iff_not_forced_box {M : Model κ} {x : M.World} {A : Formula} : ¬x ⊩ □A ↔ ∃ y, x ≺ y ∧ ¬y ⊩ A := by grind;

@[grind]
def Formula.Valid (M : Model κ) (A : Formula) : Prop := ∀ x : M.World, x ⊩ A
infix:50 " ⊧ " => Formula.Valid


def FormulaFinset.Forced {M : Model κ} (x : M.World) (Γ : FormulaFinset) : Prop := ∀ A ∈ Γ, x ⊩ A
infix:55 " ⊩ " => FormulaFinset.Forced


@[grind]
def Sequent.Forced {M : Model κ} (x : M.World) (S : Sequent) : Prop := (∀ C ∈ S.ant, x ⊩ C) → (∃ D ∈ S.suc, x ⊩ D)
infix:55 " ⊩ " => Sequent.Forced

lemma Sequent.forced_succ_singleton {M : Model κ} {x : M.World} : x ⊩ (Γ ⟹ {A}) ↔ (∀ C ∈ Γ, x ⊩ C) → x ⊩ A := by grind;

@[grind]
def Sequent.Valid (M : Model κ) (S : Sequent) : Prop := ∀ x : M.World, x ⊩ S
infix:50 " ⊧ " => Sequent.Valid


section soundness

variable {M : Model κ} {Γ Γ' Δ Δ' : FormulaFinset} {A B : Formula}

lemma valid_axm : M ⊧ ({A} ⟹ {A}) := by
  intro x h;
  use A;
  constructor;
  . grind;
  . exact h _ (by grind);

lemma valid_botL : M ⊧ ({⊥} ⟹ ∅) := by
  intro x;
  simp [Sequent.Forced, Formula.Forced];

lemma valid_wkL (h : M ⊧ (Γ ⟹ Δ)) (hΓ : Γ ⊆ Γ' := by grind) : M ⊧ (Γ' ⟹ Δ) := by
  intro x h';
  apply h;
  grind;

lemma valid_wkR (h : M ⊧ (Γ ⟹ Δ)) (hΔ : Δ ⊆ Δ' := by grind) : M ⊧ (Γ ⟹ Δ') := by
  intro x hΓ;
  obtain ⟨D, hD₁, hD₂⟩ := h x hΓ;
  grind;

lemma valid_impL (hA : M ⊧ (Γ ⟹ insert A Δ)) (hB : M ⊧ (insert B Γ ⟹ Δ)) : M ⊧ ((insert (A 🡒 B) Γ) ⟹ Δ) := by
  intro x h;
  replace hA := hA x
  replace hB := hB x;
  simp only [Finset.mem_insert, forall_eq_or_imp] at h;
  grind;

lemma valid_impR (h : M ⊧ ((insert A Γ) ⟹ (insert B Δ))) : M ⊧ (Γ ⟹ (insert (A 🡒 B) Δ)) := by
  intro x hΓ;
  by_cases x ⊩ A;
  . obtain ⟨D, hD₁, hD₂⟩ := h x $ by grind;
    simp at hD₁;
    rcases hD₁ with (rfl | hD₁);
    . use A 🡒 D; grind;
    . use D; grind;
  . use A 🡒 B;
    grind;


namespace Model

abbrev _root_.IsConverseWellFounded (α) (R : α → α → Prop) := IsWellFounded α (λ x y => R y x)

lemma has_terminal [IsConverseWellFounded _ M.Rel'] : ∀ (W : Set M.World), Set.Nonempty W → ∃ t ∈ W, ∀ x ∈ W, ¬(t ≺ x) :=
  WellFounded.wellFounded_iff_has_min.mp (by apply IsWellFounded.wf)

class IsGL (M : Model κ) extends IsTrans _ M.Rel', IsConverseWellFounded _ M.Rel'

class IsFiniteGL (M : Model κ) extends Fact (Finite M.World), IsTrans _ M.Rel', Std.Irrefl M.Rel'

end Model



lemma valid_boxGL [M.IsGL] (h : M ⊧ ((insert (□A) (Γ ∪ Γ.box)) ⟹ {A})) : M ⊧ (Γ.box ⟹ {□A}) := by
  intro x;
  apply Sequent.forced_succ_singleton.mpr;
  intro hΓ y Rxy;
  apply Sequent.forced_succ_singleton.mp $ h y;
  simp only [Finset.mem_insert, Finset.mem_union, Finset.mem_image, forall_eq_or_imp];
  refine ⟨?_, ?_⟩;
  . by_contra hC;
    obtain ⟨z, Ryz, hz⟩ := Formula.iff_not_forced_box.mp hC;
    obtain ⟨t, ⟨Ryt, hntA⟩, ht₂⟩ := M.has_terminal ({z | y ≺ z ∧ ¬z ⊩ A}) ⟨z, ⟨Ryz, hz⟩⟩;
    apply hntA;
    apply Sequent.forced_succ_singleton.mp $ h t;
    simp;
    constructor;
    . rintro t' Rtt';
      by_contra;
      exact ht₂ t' ⟨_root_.trans Ryt Rtt', by assumption⟩ Rtt';
    . rintro C (hC | ⟨C, hC, rfl⟩);
      . apply hΓ (□C) (by simpa) t;
        apply _root_.trans Rxy Ryt;
      . intro t' Rtt';
        apply hΓ (□C) (by simpa) t';
        apply _root_.trans (_root_.trans Rxy Ryt) Rtt';
  . rintro C (hC | ⟨C, hC, rfl⟩);
    . exact hΓ (□C) (by simpa) y Rxy;
    . intro z Ryz;
      exact hΓ (□C) (by simpa) z (_root_.trans Rxy Ryz);

theorem soundness (h : ⊢ S) : ∀ {κ}, ∀ M : Model κ, [M.IsGL] → M ⊧ S := by
  obtain ⟨p⟩ := h;
  intro _ M M_finiteGL;
  induction p with
  | axm A => exact valid_axm
  | botL => exact valid_botL
  | wkL h _ ih => exact valid_wkL ih;
  | wkR h _ ih => exact valid_wkR ih;
  | impL _ _ ih₁ ih₂ => exact valid_impL ih₁ ih₂
  | impR _ ih => exact valid_impR ih
  | boxGL _ ih => exact valid_boxGL ih


def trivial_GL_model : Model (Fin 1) where
  Rel' := λ _ _ => False
  Val := λ _ _ => False

instance : trivial_GL_model.IsGL where
  trans x y z hxy hyz := by tauto;
  wf := @Finite.wellFounded_of_trans_of_irrefl (Fin 1) inferInstance _ ⟨by tauto⟩ ⟨by tauto⟩

lemma not_provable_empty : ⊬ (∅ ⟹ ∅) := by
  by_contra h;
  simpa [Sequent.Forced] using (soundness h trivial_GL_model) 0;

end soundness

end Semantics
